define flow main
flow
  use troy::connectors;
  define connector cb_check from cb
  with
    codec = "json",
    preprocessors = ["lines"],
    config = {
      "path": "in.json",
      "timeout": 1000,
      "chunk_size": 5
    }
  end;

  define pipeline main1
  pipeline

    define script drop_script
    script
        drop;
    end;

    create script drop_script;

    create stream dead_end;

    select event from in where event == "dead_end" into dead_end;
    # keep this here, so we keep the graph nodes around and not optimize them away
    select event from dead_end into out/not_connected;

    select event from in where event == "drop" into drop_script;
    # no event should ever go here, actually
    select event from drop_script into out;

    select event from in where event == "not_connected_port" into out/not_connected;
    select event from in where event == "passthrough" into out;
  end;

  create pipeline main1;
  create connector cb_check;
  create connector console from connectors::console;

  connect /connector/cb_check/out to /pipeline/main1/in;
  connect /pipeline/main1/out to /connector/console/in;
end;

deploy flow main;
define flow main
flow
  use std::time::nanos;
  use integration;
  use tremor::{connectors, pipelines};

  define pipeline main
  pipeline
  define script process_batch_item
    script
      # setting required metadata for elastic
      let $elastic = {
        "_index": "my_little_index",
        "action": event.action
      };
      let $correlation = event.snot;
      match event of
        case %{present doc_id} => let $elastic["_id"] = event.doc_id
        case _ => null
      end;
      event
    end;
    create script process_batch_item;

    define operator batch from generic::batch with
      count = 6
    end;
    create operator batch;


    select event from in into process_batch_item;
    select event from process_batch_item into batch;
    select event from batch into out;

    select event from process_batch_item/err into err;
  end;

  define pipeline response_handling
  pipeline
    select {
      "action": $elastic.action,
      "success": $elastic.success,
      "payload": event.payload,
      "index": $elastic["_index"],
      "correlation": $correlation
    }
    from in where $elastic.success into out;

    select {
      "action": $elastic.action,
      "payload": event.payload,
      "success": $elastic.success,
      "index": $elastic["_index"],
      "correlation": $correlation
    }
    from in where not $elastic.success into err;  
  end;

  define connector elastic from elastic
  with
    config = {
      "nodes": ["http://127.0.0.1:9200/"],
      "include_payload_in_response": true
    }
  end;

  define connector input from cb
  with
    config =  {
      "paths": ["in1.json", "in2.json"],
      "timeout": nanos::from_seconds(5),
      "expect_batched": true,

    }
  end;

  create connector errfile from integration::write_file
  with
    file = "err.log"
  end;
  create connector okfile from integration::write_file
  with
    file = "ok.log"
  end;
  create connector elastic;
  create connector input;

  create pipeline main;
  create pipeline response_handling;

  connect /connector/input/out to /pipeline/main/in;
  connect /pipeline/main/out to /connector/elastic/in;
  connect /connector/elastic/out to /pipeline/response_handling/in;
  connect /pipeline/response_handling/out to /connector/okfile/in;
  connect /connector/elastic/err to /pipeline/response_handling/in;
  connect /pipeline/response_handling/err to /connector/errfile/in;
end;
deploy flow main;